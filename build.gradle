buildscript {
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.4.1.RELEASE"
        classpath "com.netflix.nebula:nebula-project-plugin:2.2.1"
    }

    repositories {
        maven {url "https://plugins.gradle.org/m2/"}
        jcenter()
        mavenCentral()
    }
}

allprojects {
    group = 'org.brownmun'
    version = System.getenv('TRAVIS') ? System.getenv('TRAVIS_BUILD_NUMBER') : 'dev'

    apply plugin: 'idea'
    apply plugin: 'jacoco'

    repositories {
        jcenter()
        mavenCentral()
    }
}

task coverage(type: JacocoReport) {
    reports {
        html {
            destination = file("$buildDir/reports/coverage")
        }
    }
}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
}

task check(dependsOn: [testReport, coverage]) {}

configure([project(':core'), project(':busun'), project(':bucs')]) {
    apply plugin: 'java'
    apply plugin: 'spring-boot'

    dependencyManagement {
        dependencies {
            dependency 'com.google.guava:guava:19.0'
            dependency 'org.apache.commons:commons-lang3:3.5'

            dependencySet(group: 'org.testcontainers', version: '1.1.6') {
                entry 'postgresql'
                entry 'jdbc'
                entry 'testcontainers'
            }
        }
    }

    dependencies {
        compile 'com.google.guava:guava'
        compile 'org.apache.commons:commons-lang3'
        compileOnly 'org.projectlombok:lombok'
    }

    bootRun {
        addResources = true

        systemProperty 'debug', 'true'
        systemProperty 'spring.profiles.active', System.getenv('TRAVIS') ? 'ci' : 'local'
        rootProject.file('.env').eachLine {
            if (!it.isEmpty() && !it.startsWith('#')) {
                environment(*it.split('='))
            }
        }
    }

    rootProject.tasks['coverage'].configure {
        sourceSets sourceSets.main
    }

    tasks.withType(Test) {
        rootProject.tasks['coverage'].executionData it
        rootProject.tasks['testReport'].reportOn it
    }
}